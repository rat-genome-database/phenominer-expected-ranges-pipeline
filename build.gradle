// create a runnable jar with jar dependencies stored in lib subdirectory
tasks.whenTaskAdded { task ->
    ['startScripts', 'distTar'].each { String skipTaskName ->
        if (task.name.contains(skipTaskName)) {
            task.enabled = false
        }
    }
}
apply plugin: 'java'
apply plugin: 'application'

// JDK 8
java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}
mainClassName="edu.mcw.rgd.phenominerExpectedRanges.Manager"
String myAppName="PhenominerExpectedRanges"

project.archivesBaseName = myAppName

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.11'
    implementation 'commons-dbcp:commons-dbcp:1.4'
    implementation 'org.apache.commons:commons-lang3:3.8'
    implementation 'org.apache.commons:commons-math3:3.6.1'
    implementation 'commons-pool:commons-pool:1.6'
    implementation 'org.apache.logging.log4j:log4j-api:2.18.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.18.0'
    implementation 'com.oracle.database.jdbc:ojdbc8:21.1.0.0'
    implementation 'org.apache.poi:poi:3.16'
    implementation 'org.springframework:spring-beans:5.0.20.RELEASE'
    implementation 'org.springframework:spring-jdbc:5.0.20.RELEASE'

    implementation fileTree(dir: 'lib', include: '*.jar')
}

jar {
    manifest {
        attributes(
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Class-Path': configurations.runtimeClasspath.collect { it.getName() }.join(' '),
                'Main-Class': mainClassName
        )
    }
}

distributions {
    main {
        distributionBaseName = myAppName
    }
}

task createDistro(type: Copy) {
    def zipFile = file('build/distributions/'+myAppName+'.zip')
    def outputDir = file("build/install")

    from zipTree(zipFile)
    into outputDir
}
createDistro.dependsOn assembleDist
